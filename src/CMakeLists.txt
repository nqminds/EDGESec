add_subdirectory(utils)
add_subdirectory(supervisor)
add_subdirectory(ap)
add_subdirectory(dhcp)
add_subdirectory(firewall)
if (USE_RADIUS_SERVICE)
  add_subdirectory(radius)
endif ()
if (USE_CRYPTO_SERVICE)
  add_subdirectory(crypt)
endif ()
if (USE_MDNS_SERVICE)
  add_subdirectory(dns)
endif ()
add_subdirectory(capture)


add_library(sqlhook SHARED sqlhook.c)
target_compile_options(sqlhook PRIVATE -fPIC)
target_include_directories(sqlhook PRIVATE ${LIBSQLITE_INCLUDE_DIR})
target_link_libraries(sqlhook PRIVATE domain os SQLite::sqlite)

add_library(engine engine.c)
target_include_directories(engine PRIVATE ${LIBSQLITE_INCLUDE_DIR})
if (USE_RADIUS_SERVICE)
  target_compile_definitions(engine PUBLIC WITH_RADIUS_SERVICE)
  target_link_libraries(engine PRIVATE radius_service)
endif ()
if (USE_CRYPTO_SERVICE)
  target_link_libraries(engine PRIVATE crypt_service)
endif ()
if (USE_MDNS_SERVICE)
  target_compile_definitions(engine PUBLIC WITH_MDNS_SERVICE)
  target_link_libraries(engine PRIVATE mdns_service)
endif ()
target_link_libraries(engine PRIVATE net log iface_mapper os sqlite_macconn_writer firewall_service eloop supervisor network_commands mac_mapper ap_service firewall_service dhcp_service)

add_library(config config.c)
target_include_directories(config PUBLIC $<TARGET_PROPERTY:iface,INCLUDE_DIRECTORIES> ${LIBSQLITE_INCLUDE_DIR})
target_link_libraries(config PRIVATE minIni os log)

if (USE_MDNS_SERVICE AND USE_CAPTURE_SERVICE)
  add_executable(mdnsf mdnsf.c)
  target_include_directories(mdnsf PRIVATE ${LIBPCAP_INCLUDE_PATH} ${PROJECT_BINARY_DIR} ${LIBSQLITE_INCLUDE_DIR})
  target_link_libraries(mdnsf PRIVATE mdns_service config minIni squeue iface_mapper log os)
  # link time optimization
  set_target_properties(mdnsf PROPERTIES INTERPROCEDURAL_OPTIMIZATION TRUE)
endif ()

add_executable(edgesec edgesec.c)
if (USE_CRYPTO_SERVICE)
  target_include_directories(edgesec PRIVATE ${PROJECT_BINARY_DIR} ${LIBOPENSSL_INCLUDE_PATH} ${LIBSQLITE_INCLUDE_DIR})
else ()
  target_include_directories(edgesec PRIVATE ${PROJECT_BINARY_DIR} ${LIBSQLITE_INCLUDE_DIR})
endif ()
target_link_libraries(edgesec PRIVATE eloop config engine minIni os hashmap)
# link time optimization
set_target_properties(edgesec PROPERTIES INTERPROCEDURAL_OPTIMIZATION TRUE)
